if (isModule.toBoolean()) {//如果是组件模式---可以单独运行
    apply plugin: 'com.android.application'
} else {//集成模式，不能单独运行
    apply plugin: 'com.android.library'
}


android {
    compileSdkVersion getRootProject().compileSdkVersion
    buildToolsVersion getRootProject().buildToolsVersion

    defaultConfig {
        if (isModule.toBoolean()) {
            applicationId "com.example.viewautosizelayout"
        }
        minSdkVersion getRootProject().minSdkVersion
        targetSdkVersion getRootProject().targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        //ARouter添加
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    // 多渠道资源文件修改
    sourceSets {
        main {
            if (isModule.toBoolean()) {//组件模式下
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {//集成模式下
                manifest.srcFile 'src/main/library/AndroidManifest.xml'
                //集成开发模式下排除debug文件夹中的所有Java文件
                java {
                    exclude 'debug/**'
                }
            }
        }
    }
}

dependencies {
    implementation project(path: ':CommonModule')
    implementation project(path: ':zztToolsLibrary')
    //ARouter添加
    annotationProcessor 'com.alibaba:arouter-compiler:1.2.2'

    // android 适配 AndroidAutoSize
    implementation 'me.jessyan:autosize:1.2.1'


    implementation 'me.yatoooon:screenadaptation:1.1.1'

}